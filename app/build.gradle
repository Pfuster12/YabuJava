apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file("$rootDir/keystore_yabu/yabu_java.jks")
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.yabu.android.yabujava"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Android essentials
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    // Google ad
    implementation 'com.google.android.gms:play-services-ads:12.0.0'
    // charts api
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    // Glide dependency
    implementation 'com.github.bumptech.glide:glide:4.3.0'
    implementation 'com.google.firebase:firebase-core:12.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.3.0'
    implementation('com.github.bumptech.glide:recyclerview-integration:4.3.0') {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    // Lifecycle, LiveData and ViewModel components
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    // Retrofit library dependencies
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    // Parceler dependencies
    implementation 'org.parceler:parceler-api:1.1.9'
    annotationProcessor 'org.parceler:parceler:1.1.9'
    // jsoup HTML parser
    implementation 'org.jsoup:jsoup:1.11.2'
    // Testing dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}


apply plugin: 'com.google.gms.google-services'